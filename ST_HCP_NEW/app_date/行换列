  CREATE TABLE [dbo].[TestRows2Columns]([Id] [int] IDENTITY(1,1) NOT NULL,[UserName] [nvarchar](50) NULL,
 [Subject] [nvarchar](50) NULL,[Source] [numeric](18, 0) NULL ) ON [PRIMARY]
 
    INSERT INTO [TestRows2Columns] ([UserName],[Subject],[Source])

    SELECT N'张三',N'语文',60 UNION ALL

    SELECT N'李四',N'数学',70 UNION ALL

    SELECT N'王五',N'英语',80 UNION ALL

    SELECT N'王五',N'数学',75 UNION ALL

    SELECT N'王五',N'语文',57 UNION ALL

    SELECT N'李四',N'语文',80 UNION ALL

    SELECT N'张三',N'英语',100



    --5：参数化动态PIVOT行转列

      -- =============================================

      -- Author: <听风吹雨>

      -- Create date: <2014.05.26>

      -- Description: <参数化动态PIVOT行转列>

      -- Blog:

      -- =============================================

      DECLARE @sql_str NVARCHAR(MAX)

      DECLARE @sql_col NVARCHAR(MAX)

      DECLARE @tableName SYSNAME --行转列表

      DECLARE @groupColumn SYSNAME --分组字段

      DECLARE @row2column SYSNAME --行变列的字段

      DECLARE @row2columnValue SYSNAME --行变列值的字段

      SET @tableName = 'TestRows2Columns'

      SET @groupColumn = 'UserName'

      SET @row2column = 'Subject'

      SET @row2columnValue = 'Source'

      --从行数据中获取可能存在的列

      SET @sql_str = N'

      SELECT @sql_col_out = ISNULL(@sql_col_out + '','','''') + QUOTENAME(['+@row2column+'])

      FROM ['+@tableName+'] GROUP BY ['+@row2column+']'

      --PRINT @sql_str

      EXEC sp_executesql @sql_str,N'@sql_col_out NVARCHAR(MAX) OUTPUT',@sql_col_out=@sql_col OUTPUT

      --PRINT @sql_col

      SET @sql_str = N'

      SELECT * FROM (

      SELECT ['+@groupColumn+'],['+@row2column+'],['+@row2columnValue+'] FROM ['+@tableName+']) p PIVOT

      (SUM(['+@row2columnValue+']) FOR ['+@row2column+'] IN ( '+ @sql_col +') ) AS pvt

      ORDER BY pvt.['+@groupColumn+']'

      --PRINT (@sql_str)

      EXEC (@sql_str)





主要应用case语句来解决行转列的问题

行转列问题主要分为两类

1)简单的行转列问题:

示例表:

id  sid           course  result

1   2005001 语文     80.0
2   2005001 数学     90.0
3   2005001 英语     80.0
4   2005002 语文     56.0
5   2005002 数学     69.0
6   2005002 英语     89.0

执行

select sid,语文=isnull(sum(case course when '语文' then result end),0),
   数学=isnull(sum(case course when '数学' then result end),0),
   英语=isnull(sum(case course when '英语' then result end),0)
   from result 
   group by sid
   order by sid

 

得出结果

sid           语文 数学 英语 

2005001 80.0  90.0  80.0
2005002 56.0  69.0  89.0

 

2)较为复杂的行转列

表1:course

id name

1 语文
2 数学
3 英语


表2:result

id sid          course  result

1 2005001 语文      80.0
2 2005001 数学      90.0
3 2005001 英语      80.0
4 2005002 语文      56.0
5 2005002 数学      69.0
6 2005002 英语      89.0

 

declare @sql varchar(8000)
set @sql='select sid'
select @sql=@sql+','+course.name+'=isnull(sum(case course when '''+course.name+''' then result end),0)'
 from course order by id 
set @sql=@sql+' from result group by sid order by sid'
print @sql
exec(@sql)

 

得出结果

sid           语文 数学 英语 

2005001 80.0  90.0  80.0
2005002 56.0  69.0  89.0


